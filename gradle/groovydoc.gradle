import java.text.SimpleDateFormat

// https://blog.mrhaki.com/2016/01/groovy-goodness-customise-groovydoc.html
sourceSets {
    groovydoc {
        resources {
            srcDir 'src/groovydoc'
        }
    }
}

/*
 * http://gradle.org/docs/current/dsl/org.gradle.api.tasks.javadoc.Groovydoc.html
 * https://github.com/evgeny-goldin/spock-extensions/blob/master/build.gradle
 */
groovydoc {
    use = true
    // groovyClasspath = configurations.compile // http://issues.gradle.org/browse/GRADLE-1391
    // Create GroovyDoc for Groovy
    if(!findProperty('testGroovydoc')) {
        source = [sourceSets.main.groovy, sourceSets.main.java]
    } else {
        source = [sourceSets.main.groovy, sourceSets.main.java, sourceSets.test.groovy]
    }
    windowTitle = "Jervis API Docs"
    docTitle = "Jervis ${version} API"
    header = '<a href="https://github.com/samrocketman/jervis" target="_blank">github.com/samrocketman/jervis</a>'
    footer = '<a href="https://github.com/samrocketman/jervis" target="_blank">Jervis</a> API documentation.'
    link 'https://docs.oracle.com/en/java/javase/11/docs/api/java.base/', 'java.'
    link 'http://docs.groovy-lang.org/latest/html/gapi/', 'groovy.', 'org.codehaus.groovy.'
    link 'https://javadoc.jenkins-ci.org/', 'hudson.', 'jenkins.'
    doLast {
        copy {
            rename "stylesheet-${groovyVersion.tokenize('.')[0..1].join('.')}.css", 'stylesheet.css'
            from "assets/stylesheet-${groovyVersion.tokenize('.')[0..1].join('.')}.css" into 'build/docs/groovydoc'
        }
        copy {
            from 'src/groovydoc/resources/highlightjs' into 'build/docs/groovydoc'
        }
    }

    // Use custom sourceSet template
    classpath = sourceSets.groovydoc.output + classpath
}
